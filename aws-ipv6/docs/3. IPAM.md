# IPAM

IPAM, or IP Address Management (or Internet Protocol Address Management) as per Microsoft is:
> IP Address Management (IPAM) is an integrated suite of tools to enable end-to-end planning, deploying, managing and monitoring of your IP address infrastructure, with a rich user experience. IPAM automatically discovers IP address infrastructure servers and Domain Name System (DNS) servers on your network and enables you to manage them from a central interface.

but that's maybe a bit much. In our case its goal is to prevent using same address twice and thus keep everything routable between each other.


## Why to use IPAM

You might have done IPAM with Excel if you have long enough background with subnets onprem, or with some markdown files, if it has been just cloud. These tools... well, they don't do the trick as they're elsewhere and way too ofter out of date and they really do nothing to help you with planning or reallocations. Effectively dealing with databases of numbers and finding where to do good splits in order to make any sense out of it is really hard and mistakes in planning can show up years after they've been made. It's also really boring and tedious task.

Using comprehensive IPAM, in this example's case from [AWS](https://docs.aws.amazon.com/vpc/latest/ipam/what-it-is-ipam.html) helps with this. There's also other self-hosted alternatives that I've worked with like [Netbox](https://docs.netbox.dev/en/stable/) that I really like, but as they don't integrate to VPCs natively, we'd have to write some sort of plugin for it, host it, keep maintaing it and make a provider for terraform for it.


### Allocations and reservations

You've likely gotten some allocations from elsewhere in the organization and that'll have to do. Then there must be effective way to furthermore allocate to teams 

## Lets see it in Terraform (opentofu, whatever)

The IPAM itself

```terraform
resource "aws_vpc_ipam" "this" {
  description = "IPAM for AWS"
  dynamic "operating_regions" {
    for_each = local.all_ipam_regions
    content {
      region_name = operating_regions.value
    }
  }
  tags = {
    "Name" = "yle-aws-ipam"
  }
}
```

Define some useful locals and variables

```terraform
locals {
  # ensure current provider region is an operating_regions entry. Note that *ALL* resources
  # from these regions will be discovered and billed for.
  all_ipam_regions = distinct(concat([data.aws_region.current.name], var.ipam_regions))
  # In order to have this in for_each, we need to toset it. Lists are not allowed in for_each
  test_cidrs       = toset([for k, v in var.ipam_cidrs : k if can(regex("test", k))])
  prod_cidrs       = toset([for k, v in var.ipam_cidrs : k if can(regex("prod", k))])
}

variable "ipam_regions" {
  description = "Regions we want to be ready to operate in"
  type    = list(any)
  default = ["us-east-1", "us-west-1"]
}

variable "ipam_cidrs" {
	description = "variables given to us by THE ORG"
  default = {
    "us-east-1-prod" = "10.1.0.0/16"
    "us-east-1-test" = "10.2.0.0/16"
    "us-west-1-prod" = "10.3.0.0/16"
    "us-west-1-test" = "10.4.0.0/16"
  }
}
```

Now we have the definiton for IPAM (as where it should operate) and the CIDRs what we can operate, lets make some pools out of them that we can assign later on.

```terraform
resource "aws_vpc_ipam_pool" "v4" {
  for_each = var.ipam_cidrs

  address_family = "ipv4"
  ipam_scope_id  = aws_vpc_ipam.this.private_default_scope_id
  # This strips the -test and -prod suffixes away, giving us the region from key.
  # Doing the stripping from -5 allows us to go for eu-north (8 char) and us-east (7 char)
  # and still have the same logic working correctly
  locale         = trimsuffix("${each.key}", substr("${each.key}", -5, 0))
  tags = {
    "Name" = each.key
  }
}

resource "aws_vpc_ipam_pool_cidr" "v4" {
  for_each = var.ipam_cidrs

  ipam_pool_id = aws_vpc_ipam_pool.v4["${each.key}"].id
  cidr         = each.value
}

resource "aws_vpc_ipam_pool" "v6" {
  for_each         = toset(local.all_ipam_regions)
  address_family   = "ipv6"
  public_ip_source = "amazon"
  ipam_scope_id    = aws_vpc_ipam.this.public_default_scope_id

  locale      = each.value
  aws_service = "ec2"
}

resource "aws_vpc_ipam_pool_cidr" "v6" {
  for_each = toset(local.all_ipam_regions)

  ipam_pool_id   = aws_vpc_ipam_pool.v6["${each.key}"].id
  netmask_length = "48"

  depends_on = [
    aws_vpc_ipam_pool.v6
  ]
}
```

Now we have our IPAM, addresses and pools set up per region so that they're ready to be used. Because we are *smort*, we'll even output the relevant info out from this state, so we can use it later on elsewhere.

Note how with `pool_id_v6` the values needs to be split. Why make the IDs comparable with each other when using the provider? I don't know, but with this one simple trick we don't need to care about it later on when we use the pools later on.

```terraform
output "pool_id_v4" {
  description = "Pool ID as a map with key in format `region-env`"
  value = {
    "us-east-1-prod" : aws_vpc_ipam_pool.v4["us-east-1-prod"].id
    "us-east-1-test" : aws_vpc_ipam_pool.v4["us-east-1-test"].id
    "us-west-1-prod" : aws_vpc_ipam_pool.v4["us-west-1-prod"].id
    "us-west-1-test" : aws_vpc_ipam_pool.v4["us-west-1-test"].id
  }
}

output "pool_id_v6" {
  description = "Pool ID as a map with key in format `region-env`"
  value = {
    "us-east-1" : split("_", aws_vpc_ipam_pool_cidr.v6["us-east-1"].id)[1]
    "us-west-1" : split("_", aws_vpc_ipam_pool_cidr.v6["us-west-1"].id)[1]
  }
}
```
